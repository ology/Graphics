#!/usr/bin/env perl
#
# Restaurant location data:
# https://docs.mongodb.org/getting-started/shell/import-data/
#
# mongod --auth
# mongo -u gene -p --authenticationDatabase "admin"
# db.restaurants.findOne({},{_id:0}) # for instance
#
use strict;
use warnings;

use YAML;
use MongoDB;
use Algorithm::TravelingSalesman::BitonicTour;
use Math::Geometry::Planar;
use Statistics::R;

my $borough = shift || 'Manhattan';
my $grade   = shift || 'A';
my $score   = shift // 70;

my $config = shift || 'creds.yaml';
my $cfg    = YAML::LoadFile($config);

my $client = MongoDB::MongoClient->new(
    host     => $cfg->{mongo}{dbhost},
    db_name  => $cfg->{mongo}{dbname},
    username => $cfg->{mongo}{dbuser},
    password => $cfg->{mongo}{dbpass},
);

my $criteria = {
    borough        => $borough,
    'grades.grade' => $grade,
    'grades.score' => { '$gt' => 0 + $score },
#    cuisine        => qr/american/i,
};

my $collection = $client->ns('test.restaurants');
my $docs       = $collection->find($criteria);

my $tsp = Algorithm::TravelingSalesman::BitonicTour->new;

my $i = 0;

while ( my $doc = $docs->next) {
    my @point = @{ $doc->{address}{coord} };
    printf "%d. %s [%s,%s]\n", ++$i, $doc->{name}, @point;
    $tsp->add_point(@point);
}

my ( $len, @coords ) = $tsp->solve;

my $polygon = Math::Geometry::Planar->new;
$polygon->points( [ @coords[ 1 .. $#coords ] ] );
my $centroid = $polygon->centroid;

# Produce a map with points and path
print "Producing map...\n";
my $R = Statistics::R->new();

$R->run( 'library(ggmap)' );

my $file = "$0.png";
$R->run( "png('$file')" );

$R->set( 'X', [ map { $_->[0] } @coords ] );
$R->set( 'Y', [ map { $_->[1] } @coords ] );
$R->run( 'df <- data.frame(X, Y)' );
$R->run( "names(df) <- c('lon','lat')" );

$R->run( "center <- c($centroid->[0], $centroid->[1])" );
$R->run( "centroid <- get_googlemap( center = center, zoom = 12 )" );

my $gg_cmd = 'ggmap( centroid, aes(lon, lat) )';
$gg_cmd .= ' + geom_point( data = df, colour = "darkred" )';
$gg_cmd .= ' + geom_path( data = df, color = "darkgray", size = 0.5 )';
$R->run($gg_cmd);

$R->run( 'dev.off()' );

$R->stop();
print "Done.\n";

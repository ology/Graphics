#!/usr/bin/env perl
use strict;
use warnings;

use Random::PoissonDisc ();
use SVG qw(title);

use constant DIST => 12; # Minimum distance between points
use constant EDGE => 8;  # Avoid the edge by this many pixels

my $items = shift || 500;
my $size  = shift || 500;

my $fill   = 'white';
my $border = 'black';

my $svg = SVG->new(
    width  => $size,
    height => $size,
);
$svg->title()->cdata('Poisson Distribution');

my $style = $svg->group(
    id    => 'style-group',
    style => {
        stroke => $border,
        fill   => $fill,
    },
);

my $points = Random::PoissonDisc->points(
    dimensions => [$size, $size],
    r          => DIST,
    avoid_edge => EDGE,
    center     => 1,
);

my $i = 0;

for my $point ($points->@*) {
    $i++;

    $style->anchor(
        id     => $i . '-anchor',
        -href  => 'https://ology.github.io/',
        target => '_blank'
    )->circle(
        cx => $point->[0],
        cy => $point->[1],
        r  => 3,
        id => $i . '-point'
    );

    last if $i > $items;
}

print $svg->xmlify;

#!/usr/bin/env perl
use strict;
use warnings;

use Random::PoissonDisc ();
use SVG qw(title);

my $size = shift || 500;
my $max  = shift || 500;

my $fill   = 'white';
my $border = 'black';

my $svg = SVG->new(
    width  => $size,
    height => $size,
);
$svg->title()->cdata('Poisson Distribution');

my $style = $svg->group(
    id    => 'style-group',
    style => {
        stroke => $border,
        fill   => $fill,
    },
);

my $points = Random::PoissonDisc->points(
    dimensions => [$size, $size],
    r          => 20,
);

my $i = 0;

for my $point ($points->@*) {
    $i++;

    $style->anchor(
        id     => $i . '-anchor',
        -href  => 'https://ology.github.io/',
        target => '_blank'
    )->circle(
        cx => $point->[0],
        cy => $point->[1],
        r  => 3,
        id => $i . '-point'
    );

    last if $i > $max;
}

print $svg->xmlify;

#!/usr/bin/env perl
use strict;
use warnings;

use GD::Simple;
use List::Util qw( min max );
use Text::CSV;

# Get the data file from the command-line
my $file = shift || die "Usage: perl $0 /data/file.csv\n";

# Open the data file for reading
open my $data, '<:encoding(utf8)', $file
    or die "Can't read $file: $!";

# Instantiate a CSV object
my $csv = Text::CSV->new( { binary => 1 } )
    or die "Can't use CSV: ", Text::CSV->error_diag();

# Get the number of data rows and the min/max of the columns
my $nrow = 0;
my %bounds;
while ( my $row = $csv->getline($data) ) {
    $nrow++;

    my $col = 0;
    for my $item ( @$row ) {
        $col++;
        push @{ $bounds{$col} }, $item;
    }
}
for my $col ( keys %bounds ) {
    $bounds{$col} = {
        min => min( @{ $bounds{$col} } ),
        max => max( @{ $bounds{$col} } ),
    };
}

# Reset the data file to read in again
seek $data, 0, 0;

# XXX This block is horribly brittle.  The image size cannot be easily adjusted at all.
# Render a data row into a Chernoff face
my $i = 0;
while ( my $row = $csv->getline($data) ) {
    $i++;

    # Read the first 5 dimensions
    my ( $headx, $mouthx, $eyesx, $eyess, $noses ) = @$row;
    my ( $bottom, $top );

    # Canvas
    my $width  = 400;
    my $height = 250;
    my $img = GD::Simple->new( $width, $height );
    $img->penSize( 1, 1 );
    $img->bgcolor(undef);
    $img->rectangle( 0, 0, $width - 1, $height - 1 );

    # Head
    $bottom = 250;
    $top    = 30;
    $headx = scale_to( $headx, $bounds{1}{min}, $bounds{1}{max}, $bottom, $top );
    $img->fgcolor('black');
    $img->moveTo( $width / 2, $height / 2 );
    $img->ellipse( $headx, $height - $height / 5 );

    # Mouth
    $bottom = 40;
    $top    = -60;
    $mouthx = scale_to( $mouthx, $bounds{2}{min}, $bounds{2}{max}, $bottom, $top );
    $img->fgcolor('darkred');
    my $x1 = ( $width - $width / 3 ) + 1 + $mouthx;
    my $x2 = ( $width / 3 ) - $mouthx;
    my $y1 = $height - $height / 3;
    $img->moveTo( $x1, $y1 );
    $img->lineTo( $x2, $y1 );

    # Eyes
    $bottom = 5;
    $top    = 55;
    $eyesx = scale_to( $eyesx, $bounds{3}{min}, $bounds{3}{max}, $bottom, $top );
    $bottom = 50;
    $top    = 5;
    $eyess = scale_to( $eyess, $bounds{4}{min}, $bounds{4}{max}, $bottom, $top );
    $img->fgcolor('darkblue');
    $x1 = ( $width / 3 ) + 1 + $eyesx;
    $y1 = $height / 3;
    $img->moveTo( $x1, $y1 );
    $img->ellipse( $eyess, $eyess );
    $x1 = ( $width - $width / 3 ) - $eyesx;
    $y1 = $height / 3;
    $img->moveTo( $x1, $y1 );
    $img->ellipse( $eyess, $eyess );

    # Nose
    my $poly = GD::Polygon->new;
    $bottom = 0;
    $top    = 36;
    $noses = scale_to( $noses, $bounds{5}{min}, $bounds{5}{max}, $bottom, $top );
    $img->fgcolor('black');
    my $defaultx = 30;
    $x1 = $width / 2;
    $y1 = $height / 3;
    $poly->addPt( $x1, $y1 );   # Top
    $x1 = ( $width / 3 ) + $defaultx + 1 + $noses;
    $y1 = $height / 2;
    $poly->addPt( $x1, $y1 );   # Left
    $x1 = ( $width - $width / 3 ) - $defaultx - $noses;
    $poly->addPt( $x1, $y1 );   # Right
    $img->polygon($poly);

    # Data
    $img->moveTo( 20, $height - 10 );
    $img->string( join ', ', map { sprintf '%.3f', $_ } @$row );

    # Write-out the image file
    my $name = sprintf '%s-%0*d.png', $0, length($nrow), $i;
    open my $fh, '>', $name or die "Can't write to $name: $!\n" ;
    binmode $fh;
    print $fh $img->png;
    close $fh;
}

# Close the CSV and data file
$csv->eof or $csv->error_diag();
close $data;

# https://stackoverflow.com/questions/5294955/how-to-scale-down-a-range-of-numbers-with-a-known-min-and-max-value
sub scale_to {
    my ( $x, $min, $max, $bottom, $top ) = @_;
    return ( ( $bottom - $top ) * ( $x - $min ) / ( $max - $min ) ) + $top;
}

__END__
# Create random data in R:
randn <- function (n=10, mini=0, maxi=100) { as.integer(trunc(runif( n, min=mini, max=maxi ))) }
df <- data.frame( a = randn(), b = randn(), c = randn(), d = randn(), e = randn() )
write.table(df, 'data.csv', quote = F, sep = ",", row.names = F, col.names = F)

# Create animated gif with imagemagick:
convert -delay 100 -size 400x250 chernoff-face-*.png chernoff-face.gif

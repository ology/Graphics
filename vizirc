#!/usr/bin/env perl
use Modern::Perl '2012';

use List::Util qw( maxstr minstr );
use Parse::IRCLog;
use GD::Graph::Polar;

my $log = shift || 'irc.log';

my $irc = Parse::IRCLog->parse($log);

my $nicks;

# Capture the number of occurances
for my $event ( $irc->events )
{
    my $nick = $event->{nick};
    next unless $nick;
    next if $nick eq 'GumbyPAN' || $nick eq 'shadowpaste' || $nick eq 'perlbot';
    push @{ $nicks->{$nick}{occurance} }, $event->{timestamp};
}

# Capture directed conversation
for my $event ( $irc->events )
{
    my $nick = $event->{nick};
    next unless $nick;
    ( my $at = $event->{text} ) =~ s/^(\w+):\s.+$/$1/;
    push @{ $nicks->{$nick}{at} }, $at
        if exists $nicks->{$nick} && exists $nicks->{$at};
}

# Compute to coordinates
my ( $theta, $r, $maxr );
for my $nick ( keys %$nicks )
{
    $r = @{ $nicks->{$nick}{occurance} };
    $maxr = $r if !$maxr || $r > $maxr;
    $theta = $nicks->{$nick}{occurance}[-1];
    $theta = time2degrees($theta);
    $nicks->{$nick}{coordinate} = [ $r, $theta ];
}

my $g = GD::Graph::Polar->new(
    size => 600,
    radius => $maxr,
    ticks => 20,
);

# Add datapoints, labels & edges
for my $nick ( keys %$nicks )
{
    my $r = $maxr - $nicks->{$nick}{coordinate}[0];
    my $t = $nicks->{$nick}{coordinate}[1];
    $g->addPoint( $r, $t );
#    $g->addString( $r, $t, $nick );
    for my $at ( @{ $nicks->{$nick}{at} } )
    {
        my $atr = $maxr - $nicks->{$at}{coordinate}[0] + 1;
        $g->addLine(
            $r, $t,
            $atr, $nicks->{$at}{coordinate}[1] 
        );
    }
}

print $g->draw;

sub time2degrees
{
    # HH:MM:SS
    my( $h, $m ) = split /:/, shift;

    # Convert minutes to degrees
    $m += $h * 60 + $m;
    my $degrees = $m * 360 / 1440; # By proportion: deg/360=min/1440 per day

    return $degrees;
}

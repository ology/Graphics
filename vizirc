#!/usr/bin/env perl
use Modern::Perl '2012';

use Parse::IRCLog;
use GD::Graph::Polar;

my $log = shift || 'irc.log';

my @interesting = @ARGV;

my $irc = Parse::IRCLog->parse($log);

my $nicks;

# Capture the number of occurances
for my $event ( $irc->events )
{
    my $nick = $event->{nick};
    next unless $nick;
    # TODO --ignore
    next if $nick eq 'GumbyPAN' || $nick eq 'shadowpaste' || $nick eq 'perlbot' || $nick eq 'Paper-bot';
    push @{ $nicks->{$nick}{occurance} }, $event->{timestamp};
}

# Capture directed conversation
for my $event ( $irc->events )
{
    my $nick = $event->{nick};
    next unless $nick;
    ( my $at = $event->{text} ) =~ s/^(\w+):\s.+$/$1/;
    push @{ $nicks->{$nick}{at} }, $at
        if exists $nicks->{$nick} && exists $nicks->{$at};
}

# Compute coordinates
my ( $theta, $r, $maxr );
for my $nick ( keys %$nicks )
{
    $r = @{ $nicks->{$nick}{occurance} };
    $maxr = $r if !$maxr || $r > $maxr;
    $theta = $nicks->{$nick}{occurance}[-1];
    $theta = time2degrees($theta);
    $nicks->{$nick}{coordinate} = [ $r, $theta ];
}

my $g = GD::Graph::Polar->new(
    size => 600,
    radius => $maxr,
    ticks => 20,
);

# Add datapoints, labels & edges
@interesting = keys %$nicks unless @interesting;
for my $nick ( @interesting )
{
    my $r = $maxr - $nicks->{$nick}{coordinate}[0] + 1;
    my $t = $nicks->{$nick}{coordinate}[1];
    add_datapoint( $r, $t, $nick, $nicks );

    for my $at ( @{ $nicks->{$nick}{at} } )
    {
        my $atr = $maxr - $nicks->{$at}{coordinate}[0] + 1;
        my $att = $nicks->{$at}{coordinate}[1];
        add_datapoint( $atr, $att, $at, $nicks ) unless $nicks->{$at}{drawn};
        # TODO Color line based on how many times the nick occurs
        $g->addLine( $r, $t, $atr, $att );
    }
}

print $g->draw;

sub add_datapoint
{
    my ( $r, $t, $nick, $nicks ) = @_;
    $g->addPoint( $r, $t );
    $g->addString( $r, $t, $nick );
    $nicks->{$nick}{drawn}++;
}

sub time2degrees
{
    # HH:MM:SS
    my( $h, $m ) = split /:/, shift;

    # Convert minutes to degrees
    $m += $h * 60;
    my $degrees = $m * 360 / 1440; # By proportion: deg/360=min/1440 per day

    return $degrees;
}
